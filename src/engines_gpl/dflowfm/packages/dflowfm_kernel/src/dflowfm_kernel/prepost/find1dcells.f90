!----- AGPL --------------------------------------------------------------------
!
!  Copyright (C)  Stichting Deltares, 2017-2024.
!
!  This file is part of Delft3D (D-Flow Flexible Mesh component).
!
!  Delft3D is free software: you can redistribute it and/or modify
!  it under the terms of the GNU Affero General Public License as
!  published by the Free Software Foundation version 3.
!
!  Delft3D  is distributed in the hope that it will be useful,
!  but WITHOUT ANY WARRANTY; without even the implied warranty of
!  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!  GNU Affero General Public License for more details.
!
!  You should have received a copy of the GNU Affero General Public License
!  along with Delft3D.  If not, see <http://www.gnu.org/licenses/>.
!
!  contact: delft3d.support@deltares.nl
!  Stichting Deltares
!  P.O. Box 177
!  2600 MH Delft, The Netherlands
!
!  All indications and logos of, and references to, "Delft3D",
!  "D-Flow Flexible Mesh" and "Deltares" are registered trademarks of Stichting
!  Deltares, and remain the property of Stichting Deltares. All rights reserved.
!
!-------------------------------------------------------------------------------

module m_find1dcells
   use network_data
   use m_alloc
   use m_flowgeom, only: xz, yz, ba
   use gridoperations
   use MessageHandling
   use m_save_ugrid_state

   implicit none

   private

   public find1dcells

contains

!> find one-dimensional net cells
!>    it is assumed that kc has been allocated
!>    it is assumed that findcells has already been called (for 2d cells)
   subroutine find1dcells()

#ifdef _OPENMP
      use omp_lib
#endif
      implicit none

      integer :: k1, k2, k3, L, N, nc1, nc2
      integer :: i, ierr, k, kcell
      integer, dimension(:), allocatable :: nc1_array, nc2_array
      logical :: Lisnew
      integer :: temp_threads
      integer :: ierror
      ierror = 1

      allocate (nc1_array(NUML1D), nc2_array(NUML1D))
#ifdef _OPENMP
      temp_threads = omp_get_max_threads() !> Save old number of threads
      call omp_set_num_threads(OMP_GET_NUM_PROCS()) !> Set number of threads to max for this O(N^2) operation
#endif
      !$OMP PARALLEL DO
      do L = 1, NUML1D
         if (KN(1, L) /= 0 .and. kn(3, L) /= 1 .and. kn(3, L) /= 6) then
            call INCELLS(Xk(KN(1, L)), Yk(KN(1, L)), nc1_array(L))
            call INCELLS(Xk(KN(2, L)), Yk(KN(2, L)), nc2_array(L))
         end if
      end do
      !$OMP END PARALLEL DO
#ifdef _OPENMP
      call omp_set_num_threads(temp_threads)
#endif

!     BEGIN COPY from flow_geominit
      KC = 2 ! ONDERSCHEID 1d EN 2d NETNODES

      do L = 1, NUML
         k1 = KN(1, L); k2 = KN(2, L); k3 = KN(3, L)
         if (k3 >= 1 .and. k3 <= 7) then
            KC(k1) = 1; KC(k2) = 1
         end if
      end do

      nump1d2d = nump
      do i = 1,2
         do L = 1, NUML1D
            k1 = KN(1, L); k2 = KN(2, L)
            if (k1 == 0) cycle
            nc1 = set_N(L,k1,nc1_array)
            nc2 = set_N(L,k2,nc2_array)
            if (nc1 /= 0 .and. nc1 == nc2) then !Both net nodes inside 2D cell, but assume that the first is then the 1D net node.
               nc1 = 0
            end if
            LNN(L) = 0
            call set_lne(nc1, k1, L, 1, nump1d2d)
            call set_lne(nc2, k2, L, 2, nump1d2d)
         end do
      end do

!     END COPY from flow_geominit

!     fill 1D netcell administration and set cell centers
      call realloc(xzw, nump1d2d)
      call realloc(yzw, nump1d2d)
      call realloc(xz, nump1d2d)
      call realloc(yz, nump1d2d)
      call realloc(ba, nump1d2d, KeepExisting=.true., fill=0d0) ! 1D ba's will be filled halfway through flow_geominit, just allocate and initialize 1D part here
      call increasenetcells(nump1d2d, 1.0, .true.)
      do k = nump + 1, nump1d2d
         netcell(k)%N = 0
         call realloc(netcell(k)%NOD, 1, stat=ierr, keepExisting=.false., fill=0)
         call realloc(netcell(k)%LIN, 1, stat=ierr, keepExisting=.false., fill=0)
      end do

      do k = 1, numk
         if (kc(k) < 0) then ! 1d cell
            nc1 = -kc(k) ! cell number
            N = netcell(nc1)%N
!           check if this node is new in this cell
            Lisnew = .true.
            do i = 1, N
               if (netcell(nc1)%nod(i) == k) then
                  Lisnew = .false.
                  exit
               end if
            end do
            if (Lisnew) then ! new node for this cell
               N = N + 1
               if (N > 1) then
                  call realloc(netcell(nc1)%NOD, N, stat=ierr, keepExisting=.true., fill=0)
                  call realloc(netcell(nc1)%LIN, N, stat=ierr, keepExisting=.true., fill=0)
               end if
               netcell(nc1)%N = N
               netcell(nc1)%nod(N) = k
            end if
         end if
      end do

      do k = 1, numk
         if (kc(k) < 0) then ! 1d cell associated with net node k
            kcell = -kc(k)
            xzw(kcell) = xk(k)
            yzw(kcell) = yk(k)
            xz(kcell) = xk(k)
            yz(kcell) = yk(k)
         end if
      end do

!     safety: 1D-cells can have negative lne, which will cause problems
      if (nump1d2d > nump) then
         netstat = NETSTAT_CELLS_DIRTY
      end if

      ierror = 0
1234  continue

      return
   end subroutine find1dcells

   logical function is_new_1D_cell(k, l)
      integer, intent(in) :: k !>  netnode number
      integer, intent(in) :: l !>  netlink number

      is_new_1D_cell = .false.

      if (KC(k) == 1) then !Node not yet touched
         if (NMK(k) > 1 .or. (kn(3, l) == 1 .or. kn(3, l) == 6)) then
            is_new_1D_cell = .true.
         end if
      end if
   end function is_new_1D_cell

   subroutine set_lne(NC, K, L, i_lne, nump1d2d)

      integer, intent(in) :: NC !< 2D cell number
      integer, intent(in) :: K !< new node number
      integer, intent(in) :: L !< index in LNE array to set
      integer, intent(in) :: i_lne !< index specifying if the left node (1) or right node (2) in LNE array is to be set
      integer, intent(inout) :: nump1d2d !< 1D netnode counter (starts at nump)

      if (NC == 0) then
         !> Nodes need to be found in the correct order. This is why we do 2 passes.
         if (meshgeom1d%nodebranchidx(k) == meshgeom1d%nodebranchidx(nump1d2d + 1) .and. meshgeom1d%nodeoffsets(k) == meshgeom1d%nodeoffsets(nump1d2d + 1)) then
            if (is_new_1D_cell(K, l)) then ! NIEUWE 1d CELL
               nump1d2d = nump1d2d + 1
               KC(K) = -nump1d2d ! MARKEREN ALS OUD
               LNE(i_lne, L) = -abs(KC(K)) ! NEW 1D CELL flag 1D links through negative lne ref
               LNN(L) = LNN(L) + 1
            else if (KC(K) /= 1) then
               LNE(i_lne, L) = -abs(KC(K)) ! NEW 1D CELL flag 1D links through negative lne ref
               LNN(L) = LNN(L) + 1
            end if
         end if
      else
         LNE(i_lne, L) = NC ! ALREADY EXISTING 2D CELL
         LNN(L) = LNN(L) + 1
      end if

   end subroutine set_lne
   
   integer pure function set_N(L,K,NC_array)
   integer, intent(in) :: L !< current link
   integer, dimension(:), intent(in) :: NC_array
   integer, intent(in) :: K !< node (attached to link)
   
         set_N = 0
         if (kn(3, L) /= 1 .and. kn(3, L) /= 6) then !These link types are allowed to have no 2D cells
            if (NMK(K) == 1) then
               set_N = NC_array(L) ! IS INSIDE 2D CELLS()
            end if
         end if
         
   end function set_N
   
end module